def resolveProjectPath(project, String name) {
    def direct = ":${name}"
    if (project.findProject(direct) != null) {
        return direct
    }
    def nested = ":WorldWeaver:${name}"
    if (project.findProject(nested) != null) {
        return nested
    }
    return direct
}

def moduleDependencies(project, List<String> depNames) {
    project.dependencies {
        depNames.each {
            api project.dependencies.project(path: resolveProjectPath(project, it))
        }
    }
}

def testDependencies(project, List<String> depNames) {
    project.dependencies {
        depNames.each {
            def dep = project.dependencies.project(path: resolveProjectPath(project, it))
            testmodImplementation dep
            testmodClientImplementation dep
        }
    }
}

def datagenDependencies(project, List<String> depNames) {
    project.dependencies {
        depNames.each {
            testmodDatagenImplementation project.dependencies.project(path: resolveProjectPath(project, it))
        }
    }
}

// Export methods by turning them into closures
ext {
    moduleDependencies = this.&moduleDependencies
    testDependencies = this.&testDependencies
    datagenDependencies = this.&datagenDependencies
}
